##### 3. 트리거 의사 레코드
- FOR EACH ROW, 즉 행 단위 트리거에서만 사용할 수 있다.
- **트리거에서는 DCL: COMMIT, ROLLBACK명령을 사용할 수 없다!!!!!!!!!!

1) :NEW 
- INSERT, UPDATE 이벤트에 사용되며, 입력이나 갱신을 위해 신규로 입력되는 자료를 참조한다.
- INSERT, UPDATE되는 행의 시작 부분이 :NEW이다.
```
 :NEW.(컬럼명)
 :NEW.MEM_ID 
```
	형태로 사용한다. (.는 소속을 의미한다)
- *DELETE에 사용 시 모든 컬럼이 NULL값으로 설정된다.

2) :OLD
- DELETE, UPDATE 이벤트에 사용되며 갱신이나 삭제되는 대상 행을 참조한다.
- INSERT에 사용 시 모든 컬럼이 NULL값으로 설정된다.

3) 트리거 함수
- EVENT를 발생시킨 DML명령이 어떤 명령인지를 판별하여 서로 다른 비지니스 로직을 처리할 때 사용, 주로 IF문의 조건으로 사용되어진다.
1. INSERTING: 이벤트가 INSERT이면 참을 반환
2. UPDATING: 이벤트가 UPDATE면 참을 반환
3. DELETING: 이벤트가 DELETE면 참을 반환

-  사용예: 오늘 날짜 's001' 회원이 상품 'P201000008'을 10개 구매한 경우 이를 처리하시오.
또, 수량을 5개로 변경한 경우와 구매 취소를 한 경우도 모두 처리하는 트리거를 작성하시오.


```
1. 'P201000008' 10개 구매
INSERT INTO CART(CART_MEMBER, CART_NO, CART_PROD, CART_QTY)
  VALUES ('s001', FN_CREATE_CART_NO(SYSDATE, 's001'), 'P201000008', 10);

```

```
2. 수량변경
 UPDATE CART
 SET CART_QTY = 3
 WHERE CART_NO = '2023120600001';

```

```
3. 구매취소 
DELETE FROM CART
WHERE CART_NO = '2023120600001';
```

```
CREATE OR REPLACE TRIGGER TG_CART_CHANGE
  AFTER INSERT OR UPDATE OR DELETE ON CART
  -- INSERT, UPDATE, DELETE라는 이벤트가 발생하면 그 뒤에 작동하는,
  -- TG_CART_CHANGE라는 트리거를 만들어 주었다.
  FOR EACH ROW
  -- 행단위 트리거이다.
 DECLARE 
  L_QTY NUMBER :=0;
  L_PROD_ID PROD.PROD_ID%TYPE;
  L_DATE DATE;
  L_MILEAGE NUMBER :=0;
  L_MID MEMBER.MEM_ID%TYPE;
 BEGIN 
 -- 이제 트리거의 내용이 기술된다.
  IF INSERTING THEN
  -- INSERT명령이 실행되었을 때 작동하는 트리거 함수.
  -- INSERT 명령일 경우 아래의 변수들이 할당된다.
   L_PROD_ID := :NEW.CART_PROD;
   L_QTY := :NEW.CART_QTY;
   L_MID := :NEW.CART_MEMBER;
   L_DATE := TO_DATE(SUBSTR(:NEW.CART_NO,1,8));
   
  ELSIF UPDATING THEN
  -- 트리거 함수에 있어서는 ELSE IF 가 아니라 ELSIF다.
   L_PROD_ID := :NEW.CART_PROD;
   L_QTY := :NEW.CART_QTY - :OLD.CART_QTY;
   L_MID := :NEW.CART_MEMBER;
   L_DATE := TO_DATE(SUBSTR(:NEW.CART_NO,1,8));   
   
  -- 신규값에서 기존값을 뺀 결과(변경값임)를
  -- 기존값에 더하여 변경되어질 수량을 구한다.
  ELSIF DELETING THEN
   L_PROD_ID := :OLD.CART_PROD;
   L_QTY := -(:OLD.CART_QTY);
   L_MID := :OLD.CART_MEMBER;
   L_DATE := TO_DATE(SUBSTR(:OLD.CART_NO,1,8));   
   
   END IF;
   
    -- 재고수불테이블 변경
   UPDATE REMAIN A
     SET A.REMAIN_O = A.REMAIN_O + L_QTY,
         A.REMAIN_J_99 = A.REMAIN_J_99 - L_QTY,
         A.REMAIN_DATE = L_DATE
         -- 이걸 각 IF마다 상황에 맞기 쓰기 번거로우니까 밖으로 뺐음
    WHERE A.PROD_ID = L_PROD_ID
      AND A.REMAIN_YEAR = '2020';
      -- PROD_ID외에도 REMAIN_YEAR 은 기본키라서 기입해야 쿼리의 실행효율이 올라감
      -- L_MID = :NEW.CART_MEMBER;
        
   -- 마일리지 변경
    -- 1. 마일리지 계산: 상품별 마일리지 * 수량
    SELECT L_QTY*PROD_MILEAGE INTO L_MILEAGE
    FROM PROD
    WHERE PROD_ID = L_PROD_ID;
    -- 2. 
   UPDATE MEMBER
    SET MEM_MILEAGE = MEM_MILEAGE+L_MILEAGE
    WHERE MEM_ID = L_MID;
   
   EXCEPTION 
   WHEN OTHERS THEN
   DBMS_OUTPUT.PUT_LINE('오류발생: ' || sqlerrm);
 
 END;

```
